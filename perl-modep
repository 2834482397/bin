#!/usr/bin/env perl
#
# name     : perl-modep, parse perl module dependencies
# author   : Xu Xiaodong <xxdlhy@gmail.com>
# license  : GPL
# created  : 2014 Apr 14
# modified : 2014 Apr 14
#

use strict;
use warnings;
use feature qw(say);
use File::Find::Rule;
use Module::Extract::Use;

my $dir = shift || '.';

my $rule = File::Find::Rule->new;
$rule->any(
    File::Find::Rule->name( '*.pl', '*.pm' ),
    File::Find::Rule->file->executable,
);
my @files = $rule->in($dir);

for my $file (@files) {
    parse($file);
}

sub parse {
    my ($file) = @_;
    say "Parsing $file ...";

    my $extor   = Module::Extract::Use->new;
    my @modules = $extor->get_modules($file);

    for my $mod (@modules) {
        next if $mod =~ /^[a-z]/;
        say "* $mod";
    }
}
